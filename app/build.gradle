// all-AudioDemo
// module build.gradle

plugins {
    id ("com.android.application")
    id ("org.jetbrains.kotlin.android") // Prevents duplicate kotlin warnings
}

android {
    namespace 'com.wsi.all_audiodemo'
    compileSdk 34

    defaultConfig {
        applicationId 'com.landenlabs.all_audiodemo'
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 60629
        versionName '6.06.29'

        // Limit languages to following list.
        resourceConfigurations += ['en', 'es']
        ndk {
            // abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }

        buildConfigField "long", "BuildTimeMilli", System.currentTimeMillis() + "L"
    }

    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", "version " + variant.versionName
    }

    signingConfigs {
        release
    }

    // Load signing config (does not work for command line builds)
    // println("Setting release=${rootDir}/release.properties")
    if (file("${rootDir}/release.properties").exists()) {
        def props = new Properties()
        props.load(new FileInputStream(file("${rootDir}/release.properties")))
        android.signingConfigs.release.storeFile = file(props.keyStore)
        android.signingConfigs.release.storePassword = props.keyStorePassword
        android.signingConfigs.release.keyAlias = props.keyAlias
        android.signingConfigs.release.keyPassword = props.keyAliasPassword
        println("Setting release to " + signingConfigs.release.storeFile.absolutePath )
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization.
            minifyEnabled true

            // List of resources removed at end of generated mapping file:
            // grep unused ./app/build/outputs/mapping/demoRelease/resources.txt
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lint {
        abortOnError false
        disable 'DefaultLocale', 'SimpleDateFormat', 'InflateParams', 'SpUsage',
                'UseCompoundDrawables', 'HandlerLeak', 'ViewConstructor', 'SetTextI18n',
                'RtlSymmetry', 'RtlHardcoded', 'ContentDescription',
                'MissingTranslation', 'ValidFragment', 'UnusedAttribute',
                'FieldCanBeLocal', 'LocalSuppress', 'HardcodedText', 'Typos'
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).tap {
            configureEach {
                // Enable verbose lint reporting during build.
                options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            }
        }
    }

    /*
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
     */
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
}

// Rename app
//  {project}-{flavor}-{buildType}.apk -> {parentProject}-{versionNameSuffix}-{buildType}-{versionStr}.apk
//  Ex:
//           app-release.apk -> flip-release-1.02.3456.apk
//           app-release.aab -> flip-release-1.4.27.aab
android.applicationVariants.configureEach {
    variant -> variant.outputs.configureEach {
        def ourname = project.parent.name
        def buildType = outputFileName.replaceAll(".*(debug|release).*", '$1')
        def versionStr = versionName.replaceAll("-.*", "")
        def flavor = variant.mergedFlavor.versionNameSuffix.toString().replace("null", "")
        // ext.flavorName = variant.name
        def newNameRoot = "${ourname}${flavor}-${buildType}-${versionStr}"
        outputFileName = "${newNameRoot}.apk"

        // sign{flavor}{buildType}Bundle
        //   Ex: signAppDebugBundle
        def bundleFinalizeTaskName = "sign${variant.name.capitalize()}Bundle"
        tasks.named(bundleFinalizeTaskName) {
            def file = finalBundleFile.asFile.get()
            def finalFile = new File(file.parentFile, "${newNameRoot}.aab")
            finalBundleFile.set(finalFile)
        }
    }
}

tasks.register('listFlavors') {
    description = 'List flavors'
    doLast {
        println("--- Flavors ---")
        /*
        android.productFlavors.all {
            flavor -> println("  " + flavor.name + "\t" + android.defaultConfig.applicationId + flavor.applicationIdSuffix)
        }
         */
        printf("   %-20s %-20s %-40s   %s\n", "--Package--", "--Name--", "--Description/Flavor--", "--isMinified--")
        project.android.applicationVariants.all { variant ->
            // def applicationId = [variant.mergedFlavor.applicationId, variant.buildType.applicationIdSuffix].findAll().join()
            printf("   %-20s %-20s %-40s   %b\n", variant.getApplicationId(), variant.name, variant.description, variant.buildType.isMinifyEnabled())
        }
    }
}

tasks.register('listrepos') {
    description = 'List repositories'
    doLast {
        println " Repositories:"
        project.repositories.each { printf("  %15s %s\n", it.name, it.url) }
    }
}
